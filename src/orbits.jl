"""
    orbit_plain(x, S[, action=^])
Compute the orbit of `x ∈ Ω` under the action of a group `G` generated by set `S`.

It is assumed that elements `G` act on `Ω` _on the right_ via `action(x, g)`.

### Input
 * `x` - point in set `Ω`,
 * `S` - finite generating set for `G = ⟨S⟩`,
 * `action` - function defining an action of `G`. Defaults to `^`.
### Output
 * `Δ::Vector` - the orbit of `x` under the action of `G`.
"""
orbit_plain(x, S::AbstractVector{<:GroupElement}, action=^) =
    orbit_plain!([x], S, action)
orbit_plain(x, s::GroupElement, action=^) = orbit_plain!([x], [s], action)

function orbit_plain!(x::AbstractVector, S::AbstractVector{<:GroupElement}, action=^)
    @assert !isempty(S) # groups need generators
    Δ_vec = x
    Δ = Set(Δ_vec)
    for δ in Δ_vec
        for s in S
            γ = action(δ, s)
            if γ ∉ Δ
                push!(Δ, γ)
                push!(Δ_vec, γ)
            end
        end
    end
    return Δ_vec
end
